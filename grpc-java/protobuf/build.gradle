description = 'gRPC: Protobuf'

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath libraries.protobuf_plugin
    }
}

dependencies {
    compile project(':grpc-core'),
            libraries.protobuf,
            libraries.guava,
            libraries.protobuf_util

    compile (libraries.google_api_protos) {
        exclude group: 'io.grpc'
        // 'com.google.api' transitively depends on 'com.google.auto.value:auto-value:1.1', which
        // breaks our annotations.
        exclude group: 'com.google.api'
    }

    compile (project(':grpc-protobuf-lite')) {
        exclude group: 'com.google.protobuf', module: 'protobuf-lite'
    }

    signature "org.codehaus.mojo.signature:java16:+@signature"
}

configureProtoCompilation()

idea {
    module {
        sourceDirs += file("${projectDir}/src/generated/main/grpc");
        sourceDirs += file("${projectDir}/src/generated/main/java");
    }
}
